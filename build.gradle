plugins {
	id 'java-gradle-plugin'
	id 'maven-publish'
	id 'com.gradle.plugin-publish' version '0.15.0'
}

buildScan {
	termsOfServiceUrl   = 'https://gradle.com/terms-of-service'
	termsOfServiceAgree = 'yes'
}

group 'io.github.rwinch.antora'
description = 'Uses com.github.node-gradle.node to automatically install Antora and run it'

sourceCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
	implementation gradleApi()
	implementation 'com.github.node-gradle:gradle-node-plugin:3.0.0'

	testImplementation platform('org.junit:junit-bom:5.7.2')
	testImplementation 'org.junit.jupiter:junit-jupiter-api'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}


pluginBundle {
	website = 'https://github.com/rwinch/antora-gradle-plugin'
	vcsUrl = 'https://github.com/rwinch/antora-gradle-plugin.git'
	description = project.description
	tags = ['antora', 'asciidoctor', 'asciidoc']
}

gradlePlugin {
	plugins {
		antoraPlugin {
			id = 'io.github.rwinch.antora'
			displayName = 'Antora Plugin'
			description = project.description
			implementationClass = 'io.github.rwinch.antora.AntoraPlugin'
		}
	}
}

tasks.withType(Test).configureEach(t ->
	t.useJUnitPlatform()
)